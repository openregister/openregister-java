apply plugin: 'java'
apply plugin: 'idea'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

group 'presentation'

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
configurations {
    compile.exclude module: 'slf4j-log4j12'
    compile.exclude module: 'slf4j-simple' // dropwizard uses logback, not slf4j-simple
}
dependencies {
    compile dropwizard, stringTemplate, jacksonCsv, jena, jerseyMedia, markdown, postgresClient, thymeleaf

    compile(dropwizard_module_java8) {
        exclude group: 'io.dropwizard'
    }
    compile(dropwizard_module_java8_jdbi) {
        exclude group: 'io.dropwizard'
    }

    testCompile dropwizardTest, jsoup, junit, jsonAssert
}

jar.baseName = 'presentation'

jar {
    doFirst {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        manifest {
            attributes(
                    "Main-Class": "uk.gov.register.presentation.app.PresentationApplication",
                    "Class-Path": configurations.runtime.findAll { !it.directory }.collect { it.name }.join(' ')
            )
        }
    }
}


task startPresentationApp << {
    startApp()
}

void startApp() {
    ProcessBuilder builder = new ProcessBuilder(
            "java",
            "-cp",
            sourceSets.test.runtimeClasspath.join(":"),
            "uk.gov.register.presentation.functional.testSupport.PresentationApplicationRunner",
            "server",
            "src/test/resources/test-app-config.yaml"
    ).directory(projectDir.absoluteFile)
    builder.redirectError(ProcessBuilder.Redirect.to(new File(projectDir, 'stderr.txt')))
    builder.redirectOutput(ProcessBuilder.Redirect.to(new File(projectDir, 'stdout.txt')))
    project.ext.runningPresentationApp = builder.start()
}

task stopPresentationApp << {
    stopApp()
}

void stopApp() {
    runningPresentationApp?.destroy()?.waitFor()
}

test {
    finalizedBy stopPresentationApp
    dependsOn << compileTestJava
    dependsOn << startPresentationApp
    doFirst {
        waitForApplicationToStart()
    }
    doLast {
        loadSchoolDataForConformance.execute()
        createConformanceTestVenv.execute()
        installConformanceTestDeps.execute()
        conformanceTest.execute()
    }
    testLogging {
        exceptionFormat = 'full'
    }
    afterTest { desc, result ->
        println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

clean.doFirst {
    delete "${rootDir}/.venv/"
}

task createConformanceTestVenv(type: Exec) {
    commandLine 'pyvenv-3.5','.venv'
}

task installConformanceTestDeps(type: Exec) {
    commandLine '.venv/bin/pip', 'install', '-r', 'requirements.txt'
}

task conformanceTest(type: Exec) {
    commandLine '.venv/bin/openregister-conformance', '--no-https', 'http://school.openregister.dev:9000'
}

assemble {
    doLast {
        new File("deploy/presentation.jar").bytes = new File("build/libs/presentation.jar").bytes
        createDeployableBundle.execute()
    }
}

task createDeployableBundle(type: Zip) {
    Map env = System.getenv()
    baseName("presentation-${env.'TRAVIS_BRANCH'}-${env.'TRAVIS_BUILD_NUMBER'}-${env.'TRAVIS_COMMIT'}")
    from('deploy/')
    include('*')
    include('scripts/*')
    destinationDir file('deployable_bundle') // directory that you want your archive to be placed in
}

def waitForApplicationToStart() {
    URL target = new URL('http://localhost:9000/')

    int counter = 0
    boolean appRunning = false
    while (!appRunning && counter++ < 10) {

        try {
            HttpURLConnection connection = (HttpURLConnection) target.openConnection()
            appRunning = connection.getResponseCode() == 200
        }
        catch (IOException ignored) {
            println "Waiting for application to start (10s max)..."
            Thread.sleep(1000)
        }
    }
}

task(run, type: JavaExec) {
    main = 'uk.gov.register.presentation.app.PresentationApplication'
    classpath = sourceSets.main.runtimeClasspath
    args = ["server", "config.yaml"]
    jvmArgs = ["-DbaseDirForTemplates=$projectDir/src/main/resources"]
}

task(loadSchoolData, type: JavaExec) {
    main = 'uk.gov.register.presentation.functional.testSupport.DBSupport'
    classpath = sourceSets.test.runtimeClasspath
    args = ["school", "school-data.jsonl", "school"]
}

task(loadSchoolDataForConformance, type: JavaExec) {
    doFirst{
        exec{
            executable "$projectDir/drop_schema.sh" args 'ft_presentation'
        }
        exec{
            executable "$projectDir/create_schema.sh" args 'ft_presentation'
        }
    }
    main = 'uk.gov.register.presentation.functional.testSupport.DBSupport'
    classpath = sourceSets.test.runtimeClasspath
    args = ["school", "school-data.jsonl", "ft_presentation"]
}
