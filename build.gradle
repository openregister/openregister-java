apply plugin: 'idea'
apply plugin: 'java'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

group 'openregister'

buildscript {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "com.github.robfletcher:compass-gradle-plugin:2.0.6"
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.github.robfletcher.compass"

def assetsDir = new File(projectDir, "src/main/resources/assets")

compass {
    importPath = files(
        new File(assetsDir, "govuk_frontend_toolkit/stylesheets"),
        new File(assetsDir, "govuk_template/source/assets/stylesheets")
    )
    sassDir = new File(assetsDir, "sass")
    cssDir = new File(assetsDir, "css")
    outputStyle = "expanded"
    force = true
    sourcemap = true
}

// because we build into src/main/resources, we need compassCompile to run before processResources
processResources.dependsOn 'compassCompile'


task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/openregister/openregister'
    }
}

//noinspection GroovyAssignabilityCheck
configurations {
    compile.exclude module: 'slf4j-log4j12'
    compile.exclude module: 'slf4j-simple' // dropwizard uses logback, not slf4j-simple
}

dependencies {
    compile dropwizard, stringTemplate, apacheCommonsCodec, awsCloudWatch
    compile jacksonCsv, jena, jerseyMedia, markdown, thymeleaf, verifiableLog
    compile jdbi, jersey_client
    compile(postgresClient) {
        exclude group: 'org.slf4j'
    }

    testCompile junit, mockito, dropwizardTest, wiremock
    testCompile dropwizardTest, jsoup, jsonAssert
}

jar.baseName = "openregister-java"

shadowJar {
    mergeServiceFiles()

    baseName = "openregister-java"
    manifest {
        attributes 'Main-Class': 'uk.gov.register.RegisterApplication'
    }
}

assemble {
    dependsOn << shadowJar
    doLast {
        new File("deploy/openregister-java.jar").bytes = shadowJar.archivePath.bytes
        createDeployableBundle.execute()
    }
}

task startAppForConformance << {
    startApp()
}

void startApp() {
    ProcessBuilder builder = new ProcessBuilder(
            "java",
            "-jar",
            relativePath(shadowJar.archivePath),
            "server",
            "src/test/resources/conformance-app-config.yaml"
    ).directory(projectDir.absoluteFile)
    builder.redirectError(ProcessBuilder.Redirect.to(new File(projectDir, 'stderr.txt')))
    builder.redirectOutput(ProcessBuilder.Redirect.to(new File(projectDir, 'stdout.txt')))
    project.ext.runningApp = builder.start()
    waitForApplicationToStart()
}

void waitForApplicationToStart() {
    URL target = new URL('http://localhost:9090/')

    int counter = 0
    boolean appRunning = false
    println "Waiting for application to start (10s max)"
    while (!appRunning && counter++ < 10) {
        try {
            HttpURLConnection connection = (HttpURLConnection) target.openConnection()
            appRunning = connection.getResponseCode() == 200
        }
        catch (IOException ignored) {
            Thread.sleep(1000)
        }
    }

    if(!appRunning){
        throw new Exception("Application start timeout (10s)")
    }
}

task stopAppForConformance << {
    stopApp()
}

void stopApp() {
    runningApp?.destroy()?.waitFor()
}

task(loadSchoolDataForConformance, type: JavaExec) {
    doFirst{
        exec{
            executable "$projectDir/drop_schema.sh" args 'ft_openregister_java'
        }
        exec{
            executable "$projectDir/create_schema.sh" args 'ft_openregister_java'
        }
    }
    main = 'uk.gov.register.functional.db.DBSupport'
    classpath = sourceSets.test.runtimeClasspath
    args = ["school", "school-data.jsonl", "ft_openregister_java"]
}

task createConformanceTestVenv(type: Exec) {
    commandLine 'pyvenv-3.5','.venv'
}

task installConformanceTestDeps(type: Exec) {
    dependsOn << createConformanceTestVenv
    commandLine '.venv/bin/pip', 'install', '-r', 'requirements.txt'
}

task runConformanceTests(type: Exec) {
    dependsOn << assemble
    dependsOn << startAppForConformance
    dependsOn << loadSchoolDataForConformance
    dependsOn << installConformanceTestDeps
    finalizedBy stopAppForConformance
    mustRunAfter test
    commandLine '.venv/bin/openregister-conformance', '--no-https', '--register', 'school', 'http://localhost:9090'
}

check {
    dependsOn << runConformanceTests
}

test {
    dependsOn << compileTestJava
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

clean.doFirst {
    delete "${rootDir}/.venv/"
}

task createDeployableBundle(type: Zip) {
    Map env = System.getenv()
    baseName("openregister-java-${env.'TRAVIS_BRANCH'}-${env.'TRAVIS_BUILD_NUMBER'}-${env.'TRAVIS_COMMIT'}")
    from('deploy/')
    include('*')
    include('scripts/*')
    destinationDir file('deployable_bundle') // directory that you want your archive to be placed in
}

task(run, type: JavaExec, dependsOn: ['classes']) {
    main = 'uk.gov.register.RegisterApplication'
    classpath = sourceSets.main.runtimeClasspath
    args = ["server", "config.yaml"]
    jvmArgs = ["-DbaseDirForTemplates=$projectDir/src/main/resources"]
}



task(loadSchoolData, type: JavaExec) {
    main = 'uk.gov.register.functional.db.DBSupport'
    classpath = sourceSets.test.runtimeClasspath
    args = ["school", "school-data.jsonl", "openregister_java"]
}

task stage(dependsOn: ['assemble'])
