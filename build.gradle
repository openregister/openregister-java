buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/robfletcher/gradle-plugins" }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
        classpath 'org.owasp:dependency-check-gradle:4.0.2'
    }
}

plugins {
    id "com.moowork.node" version "1.2.0"
}

node {
    version = '10.14.2'
    download = true
}

apply plugin: 'idea'
apply plugin: 'java'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

group 'openregister'

apply plugin: 'com.github.ben-manes.versions'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    analyzers {
        nodeEnabled=false
        nodeAuditEnabled=false
        bundleAuditEnabled=false
    }
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
}

// because we build into src/main/resources, we need compileSass to run before processResources
processResources.dependsOn 'compileSass'

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/openregister/openregister'
    }
    maven {
        url 'https://dl.bintray.com/alphagov/maven'
    }
}

//noinspection GroovyAssignabilityCheck
configurations {
    compile.exclude module: 'slf4j-log4j12'
    compile.exclude module: 'slf4j-simple' // dropwizard uses logback, not slf4j-simple
}

dependencies {
    compile dropwizard, stringTemplate, apacheCommonsCodec, apacheCommonsCollections, apacheCommonsLang3, awsS3, jaxb, javaxActivation
    compile jacksonCsv, jacksonDatabind, jerseyMedia, markdown, thymeleaf, verifiableLog, objectHash
    compile jdbi, jersey_client
    compile dropwizardLogstash
    compile(postgresClient) {
        exclude group: 'org.slf4j'
    }
    compile ("io.dropwizard:dropwizard-lifecycle"){
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile "com.google.guava:guava:27.0-jre"
    compile jetty

    testCompile junit, mockito, dropwizardTest, wiremock
    testCompile dropwizardTest, jsoup, jsonAssert
}

jar.baseName = "openregister-java"

shadowJar {
    mergeServiceFiles()
    exclude "license/**"

    baseName = "openregister-java"
    manifest {
        attributes 'Main-Class': 'uk.gov.register.RegisterApplication'
    }
}

assemble {
    dependsOn << shadowJar
    doLast {
        new File("deploy/openregister-java.jar").bytes = shadowJar.archivePath.bytes
    }
}

task compileSass(type: NpmTask) {
    dependsOn << npmInstall
    args = ['run', 'sass:compile']
}

task startAppForConformance {
    doLast {
        startApp()
    }
}

void startApp() {
    ProcessBuilder builder = new ProcessBuilder(
            "java",
            "-jar",
            relativePath(shadowJar.archivePath),
            "server",
            "src/test/resources/conformance-app-config.yaml"
    ).directory(projectDir.absoluteFile)
    builder.redirectError(ProcessBuilder.Redirect.to(new File(projectDir, 'stderr.txt')))
    builder.redirectOutput(ProcessBuilder.Redirect.to(new File(projectDir, 'stdout.txt')))
    project.ext.runningApp = builder.start()
    waitForApplicationToStart()
}

void waitForApplicationToStart() {
    URL target = new URL('http://localhost:9091/healthcheck')

    int counter = 0
    boolean appRunning = false
    println "Waiting for application to start (10s max)"
    while (!appRunning && counter++ < 10) {
        try {
            HttpURLConnection connection = (HttpURLConnection) target.openConnection()
            appRunning = connection.getResponseCode() == 200
        }
        catch (IOException ignored) {
            Thread.sleep(1000)
        }
    }

    if (!appRunning) {
        throw new Exception("Application start timeout (10s)")
    }
}

task stopAppForConformance {
    doLast {
        stopApp()
    }
}

void stopApp() {
    runningApp?.destroy()?.waitFor()
}

task(loadCountryDataForConformance, type: Exec) {
    doFirst {
        exec {
            executable "$projectDir/drop_schema.sh"
            args 'http://localhost:9090', 'foo:bar'
        }
    }
    mustRunAfter startAppForConformance
    commandLine 'curl', '-u', 'foo:bar', '-s', '-H', 'Content-type: application/uk-gov-rsf', '--data-binary', '@country-data.rsf',
            'http://localhost:9090/load-rsf'
}

task createConformanceTestVenv(type: Exec) {
    commandLine 'pyvenv', '.venv'
}

task installConformanceTestDeps(type: Exec) {
    dependsOn << createConformanceTestVenv
    commandLine '.venv/bin/pip', 'install', '-r', 'requirements.txt'
}

task runConformanceTestsV1(type: Exec) {
    dependsOn << assemble
    dependsOn << startAppForConformance
    dependsOn << loadCountryDataForConformance
    dependsOn << installConformanceTestDeps
    mustRunAfter test
    commandLine '.venv/bin/openregister-conformance', '--api-version', '1', '--no-https', '--register', 'country', 'http://localhost:9090/v1/', '--register-domain', 'test.register.gov.uk'
}

task runConformanceTestsV2(type: Exec) {
    dependsOn << runConformanceTestsV1
    finalizedBy stopAppForConformance
    commandLine '.venv/bin/openregister-conformance', '--api-version', '2', '--no-https', '--register', 'country', 'http://localhost:9090/next/', '--register-domain', 'test.register.gov.uk'
}

check {
    dependsOn << runConformanceTestsV1
    dependsOn << runConformanceTestsV2
    dependsOn << 'dependencyCheckAnalyze'
}

test {
    dependsOn << compileTestJava
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

clean.doFirst {
    delete "${rootDir}/.venv/"
}

task createDeployableBundle(type: Zip) {
    baseName('openregister-java')
    from('deploy/')
    include('*')
    include('manifests/**/*.yml')
    destinationDir file('deployable_bundle') // directory that you want your archive to be placed in
}

assemble.finalizedBy createDeployableBundle

task(run, type: JavaExec, dependsOn: ['classes']) {
    main = 'uk.gov.register.RegisterApplication'
    classpath = sourceSets.main.runtimeClasspath
    args = ["server", "config.yaml"]
    jvmArgs = ["-DbaseDirForTemplates=$projectDir/src/main/resources"]
}

task stage(dependsOn: ['assemble'])